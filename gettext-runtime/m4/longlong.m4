# longlong.m4 serial 1 (gettext-0.12)
dnl Copyright (C) 2002-2003 Free Software Foundation, Inc.
dnl This file is free software, distributed under the terms of the GNU
dnl General Public License.  As a special exception to the GNU General
dnl Public License, this file may be distributed as part of a program
dnl that contains a configuration script generated by Autoconf, under
dnl the same distribution terms as the rest of that program.

dnl From Bruno Haible.
dnl Test whether the compiler supports the 'long long' type.
dnl Prerequisite: AC_PROG_CC

AC_DEFUN([gt_TYPE_LONGLONG],
[
  AC_CACHE_CHECK([for long long], gt_cv_c_long_long,
    [if test "$GCC" = yes; then
       gt_cv_c_long_long=yes
     else
       AC_TRY_COMPILE([long long foo = 0LL;
         int array [2*(sizeof(long long) >= sizeof(long)) - 1];
         ], ,
         gt_cv_c_long_long=yes, gt_cv_c_long_long=no)
     fi])
  if test $gt_cv_c_long_long = yes; then
    AC_DEFINE(HAVE_LONG_LONG, 1, [Define if you have the 'long long' type.])
  fi
])
